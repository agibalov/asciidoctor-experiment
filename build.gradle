buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.2.1'
    }
}

group 'me.loki2302'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '2.9'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.jruby-gradle.base'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.github.stefanbirkner:system-rules:1.15.1'
    testCompile 'com.github.spullara.mustache.java:compiler:0.9.1'
    testCompile 'com.google.guava:guava:19.0'
    gems 'rubygems:asciidoctor-diagram:1.5.0'
    compile 'org.asciidoctor:asciidoctorj:1.5.4'
    compile 'org.jgrapht:jgrapht-core:0.9.2'
    compile group: 'org.jgrapht', name: 'jgrapht-ext', version: '0.9.2'
    compile 'com.thoughtworks.qdox:qdox:2.0-M4'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    systemProperty 'snippetsDir', snippetsDir
    outputs.dir snippetsDir
}

asciidoctor {
    sourceDir 'src/main/asciidoc'
    sources {
        include 'helloWorld.adoc'
        include 'personService.adoc'
        include 'diagram-test.adoc'
        include 'extension-test.adoc'
        include 'stuff.adoc'
    }
    attributes = [
            sourceDir: file('src'),
            snippetsDir: snippetsDir
            ]
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    dependsOn jrubyPrepare, test

    extensions {
        block(name: 'dummy', contexts: [':paragraph']) { parent, reader, attributes ->
            createBlock(parent, 'paragraph', 'Extension says hi', attributes, [:])
        }
    }
}
